buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
        springRestDocsVersion = '1.1.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'bankingapp'
	version = '0.0.1-SNAPSHOT'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.hibernate:hibernate-java8:5.0.11.Final')

    compile('org.modelmapper:modelmapper:0.7.5')
    compile('org.postgresql:postgresql:9.4-1206-jdbc42')

    // redis
    compile('org.springframework.session:spring-session-data-redis:1.3.0.RELEASE')
    compile('biz.paluch.redis:lettuce:4.3.0.Final')

	runtime('com.h2database:h2')

    testCompile('org.mockito:mockito-core:2.7.1')
    testCompile('com.jayway.jsonpath:json-path')
	testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test:4.2.1.RELEASE')
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:${springRestDocsVersion}")
}

ext {
    snippetsDir = file('build/generated-snippets')
}


test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}

bootRun { systemProperties = System.properties }
